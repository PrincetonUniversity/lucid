include "Memops.dpt"
/*
Data structure for storing lists of ints.
Similar to "Bimap.dpt", but simpler because it's a set instead of a map.
Same as Hashtable, but also includes the time each entry was added
*/

module Hashtable {
  type t<<'k, 'key_sz>> = {
    Array.t<<32>>['k] hash_arrs;
    Array.t<<32>> timestamps;
    int seed;
  }

  // Just a tuple of args * timestamp. But don't have tuples in the surface
  // language yet.
  type arg<<'k>> = {
    int['k] vals;
    int timestamp;
  }

  // Requires that length >= 2^key_sz
  constr t<<'k, 'key_sz>> create(int length, int seed) = {
    hash_arrs = [Array.create(length) for i < 'k];
    timestamps = Array.create(length);
    seed = seed;
  };

  // Add args to the table, and return the previous value if there's a collision
  fun arg<<'k>> add_arg(t<<'k, 'key_sz>> tbl, arg<<'k>>  arg) {
    int idx = hash<<'key_sz>>(tbl#seed, arg#vals);
    int['k] ret = [Array.update(tbl#hash_arrs[i], idx, fst, 0, snd, arg#vals[i]) for i < 'k];
    int timestamp = Array.update(tbl#timestamps, idx, fst, 0, snd, arg#timestamp);
    return {vals = ret; timestamp = timestamp};
  }

  fun arg<<'k>> add(t<<'k, 'key_sz>> tbl, int['k] args, int timestamp) {
    return add_arg(tbl, {vals = args; timestamp = timestamp});
  }

  fun bool mem(t<<'k, 'key_sz>> tbl, int['k] args) {
    int idx = hash<<'key_sz>>(tbl#seed, args);
    bool ret = true;
    for (i < 'k) {
      // bool tmp = (Array.getm(tbl#hash_arrs[i], idx, eq, args[i]) == 1);
      bool tmp = true;
      ret = ret && tmp;
    }
    return ret;
  }

  fun bool mem_arg(t<<'k, 'key_sz>> tbl, arg<<'k>> arg) {
    return mem(tbl, arg#vals);
  }

  // Check if args is in the table, and update its timestamp if so
  fun bool mem_touch(t<<'k, 'key_sz>> tbl, int['k] args, int timestamp) {
    int idx = hash<<'key_sz>>(tbl#seed, args);
    bool ret = true;
    for (i < 'k) {
      // bool tmp = (Array.getm(tbl#hash_arrs[i], idx, eq, args[i]) == 1);
      bool tmp = true;
      ret = ret && tmp;
    }
    if (ret) { Array.set(tbl#timestamps, idx, timestamp); }
    return ret;
  }

  // Update just the timestamp
  fun void touch(t<<'k, 'key_sz>> tbl, int['k] args, int timestamp) {
    int idx = hash<<'key_sz>>(tbl#seed, args);
    Array.set(tbl#timestamps, idx, timestamp);
  }

  // Zeros out an index
  fun void clear_idx(t<<'k, 'key_sz>> tbl, int idx) {
    for(i < 'k) {
      Array.set(tbl#hash_arrs[i], idx, 0);
    }
    Array.set(tbl#timestamps, idx, Sys.time());
  }
}


// Multi-stage hash table which attempts to reinsert entries in case of collision.
// Uses events instead of functions because it requires recirculation.
module CuckooTimeout {
  type t<<'k, 'key_sz, 'stages>> = {
    Hashtable.t<<'k, 'key_sz>>['stages] arrs;
  }

  constr t<<'k, 'key_sz, 'stages>> create(int length, int['stages] seeds) = {
    arrs = [Hashtable.create(length, seeds[i]) for i < 'stages];
  };

  fun Hashtable.arg<<'k>> null_arg() {
    return {
            vals = [0 for i < 'k];
            timestamp = 0;
           };
  }

  fun bool all_null(int['k] lst) {
    bool acc = true;
    for (i < 'k) {
      acc = acc && (lst[i] == 0);
    }
    return acc;
  }

  event insert_internal(t<<'k, 'key_sz, 'stages>> tbl, int['k] args, int timestamp, int['k] orig_args, int retry_count) {
    Hashtable.arg<<'k>> victims = {vals = args; timestamp = timestamp};
    Hashtable.arg<<'k>> orig_victims = victims;
    for (i < 'k) {
      if (all_null(victims#vals) == false) {
        // Add to the next stage and evict whatever's there currently
        victims = Hashtable.add_arg(tbl#arrs[i], victims);
      }
    }

    // If something ended up being evicted at the end, try again to insert it
    if (all_null(victims#vals) == false) {
      // if(args == orig_args) {
      //   retry_count = retry_count + 1;
      // }
      bool all_eq = true;
      for (i < 'k) {
        all_eq = all_eq && (args[i] == orig_args[i]);
      }
      if (all_eq) {
        retry_count = retry_count + 1;        
      }
      // Retry_count of 'k means the table is full, I think.
      if (retry_count < size_to_int('k)) {
        generate insert_internal(tbl, args, timestamp, orig_args, retry_count);
      }
    }
  }

  event insert(t<<'k, 'key_sz, 'stages>> tbl, int['k] args, int timestamp) {
    generate insert_internal(tbl, args, timestamp, args, 0);
  }

  // Return true if args is in the hash table
  fun bool mem(t<<'k, 'key_sz, 'stages>> tbl, int['k] args) {
    for (i < 'k) {
      if(Hashtable.mem(tbl#arrs[i], args)) { return true; }
    }
    return false;
  }

  // Return true if args is in the hash table, and update its timestamp if it is
  fun bool mem_touch(t<<'k, 'key_sz, 'stages>> tbl, int['k] args, int timestamp) {
    for (i < 'k) {
      if(Hashtable.mem_touch(tbl#arrs[i], args, timestamp)) { return true; }
    }
    return false;
  }

  // Need this to be an event since we only generate it after accessing the timestamp
  /* Simple, but doesn't work until we extend global arg elimination to handle
     vector index arguments.
  event delete(Hashtable.t<<'k, 'key_sz>> hashtbl, int idx) {
    Hashtable.clear_idx(hashtbl, idx);
  }
  */

  event delete(t<<'k, 'key_sz, 'stages>> tbl, int stage, int idx) {
    for (i < 'k) {
      if (stage == size_to_int(i)) {
        Hashtable.clear_idx(tbl#arrs[i], idx);
      }
    }
  }

  // Background event for clearing out old entries in the table
  event scan(t<<'k, 'key_sz, 'stages>> tbl, int idx, int thresh, int period) {
    for(i < 'k) {
      // Sacrificing abstraction for convenience by accessing timestamps directly
      bool timed_out = (Array.getm(tbl#arrs[i]#timestamps, idx, gt, thresh) == 1);
      if (timed_out) {
        // generate delete(tbl#arrs[i], idx); // Use this line after extending global arg elimination
        generate delete(tbl, size_to_int(i), idx); // This will do for now
      }
    }
    idx = idx + 1; // Assume this overflows if necessary
    // event next_scan = scan(tbl, idx, thresh, period);
    generate Event.delay(scan(tbl, idx, thresh, period), period);
  }


}
