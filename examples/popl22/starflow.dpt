include "Memops.dpt"
// Starflow telemetry cache.
/*
A P4 program passes control to *Flow by generating a "monitor_pkt" event.
This calls the "monitor_pkt" handler in this program (around line 75)
That handler may generate two other events:
  1. GPV records to collection servers (the events short_record and long_record)
  2. A memory release event (free_block) that gets recirculated
*/

const int h1_poly = 10398247;
const int H_SLOTS = 1024;
const int L_SLOTS = 128;
const int SIZE_SHORT = 2;
const int MAX_IDX = 7;
const int MMATCH = 0;

entry event monitor_pkt(int src, int len);

// records that the cache emits when something gets evicted.
const int COLLECTION_SERVER = 1234;
exit event short_record(int src, int len, int[2] recs);
exit event long_record(int src, int len, int[8] recs);

// free a "long slot" by pushing it's index back onto the stack.
event free_block(int block_id);

global Array.t<<32>> srcarr = Array.create(H_SLOTS);
global Array.t<<32>> ctarr = Array.create(H_SLOTS);
global Array.t<<32>> idxarr = Array.create(H_SLOTS);

global Array.t<<32>> stack_top = Array.create(1);
global Array.t<<32>> longblock_stack = Array.create(L_SLOTS);
global Array.t<<32>> block_map = Array.create(H_SLOTS);

global Array.t<<32>>[8] rec = [Array.create(H_SLOTS) for i < 8];

// increment index with rollover.
memop incr_idx (int stored, int nothingval) {
  if (stored == MAX_IDX) {
    return 0;
  } else {
    return stored + 1;
  }
}

// We need to get the index, and also the count.
memop decr_sat (int stored, int nothingval) {
  if (stored == 0) {
    return 0;
  } else {
    return stored - 1;
  }
}

// Helper function for monitor_pkt. Could be replaced with a ternary if expression
// if we add it
fun int update_or_get(Array.t<<32>> rec, int h1, int len, bool update) {
  if (update) {
    return Array.update(rec, h1, fst, 0, snd, len);
  } else {
    return Array.get(rec, h1);
  }
}

handle monitor_pkt(int src, int len) {
  int ct = 0; int idx = 0; // idx = (count % num_rec_slots)
  int[8] stored_recs = [0; 0; 0; 0; 0; 0; 0; 0];
  int blk_id = 0;
  // update the key.
  int h1 = hash<<32>>(h1_poly, src);
  int stored_src = Array.update(srcarr, h1, fst, 0, snd, src);
  // update the count and index (if its a hit).
  int mtch = src - stored_src;
  if (mtch == MMATCH) {
    ct = Array.update(ctarr, h1, sum, 1, sum, 1);
    idx = Array.update(idxarr, h1, incr_idx, 1, incr_idx, 1);
  }
  // take care of memory allocation ahead of time.
  // each flow gets to try and allocate a block once
  //  -- when it reaches this size.
  if (ct == SIZE_SHORT) {
    int stack_idx = Array.update(stack_top, 0, fst, 0, decr_sat, 1);
    // got an extended block!
    if (stack_idx != 0) {
      blk_id = Array.get(longblock_stack, stack_idx);
      Array.set(block_map, h1, blk_id);
    }
  } else {
    // if we are not at this count, then we want to see if we have a block id.
    blk_id = Array.get(block_map, h1);
  }
  if (blk_id == 0) {
    // idx = idx mod SIZE_SHORT, but mod isnt implemented yet.
    match idx with
    | 2 -> { idx = 0; }
    | 4 -> { idx = 0; }
    | 6 -> { idx = 0; }
    | 3 -> { idx = 1; }
    | 5 -> { idx = 1; }
    | 7 -> { idx = 0; }
  }
  if (mtch == MMATCH) {
    stored_recs = [update_or_get(rec[i], h1, len, idx == size_to_int(i)) for i < 8];
  } else {  // No match, just fst for evict.
    stored_recs = [Array.get(rec[i], h1) for i < 8];
  }

  // We also need to generate (and implement) the memory free event...
  // finally, generate some events depending on whether there was an evict or buffer rollover.
  if (mtch == MMATCH) {
    if (idx == 0) {
      if (blk_id == 0) {
        generate_switch (COLLECTION_SERVER, short_record(src, len, [stored_recs[0]; stored_recs[1]]));
      } else {
        generate_switch (COLLECTION_SERVER, long_record(src, len, stored_recs));
      }
    }
  } else {
    // if there wasn't a match, we need to generate a new record no matter what.
    // Note: at this point, we could zero out the invalid entries too. Original starflow implementation assumes the server takes care of it.
    if (blk_id == 0) {
      generate_switch (COLLECTION_SERVER, short_record(src, len, [stored_recs[0]; stored_recs[1]]));
    } else {
      generate_switch (COLLECTION_SERVER, long_record(src, len, stored_recs));
      // in this case, we also have to free the long block for another flow -- do that with a control event.
      generate free_block(blk_id);
    }
  }
}

handle free_block(int block_id) {
  // get a pointer to an unused slot in the free stack.
  int stack_idx = Array.update(stack_top, 0, sum, 1, sum, 1);
  // snd the block id to the unused slot.
  Array.set(longblock_stack, stack_idx, block_id);
}
