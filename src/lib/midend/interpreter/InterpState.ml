(* Interpreter context + helpers for data structure interpretation. *)
open CoreSyntax
open InterpSyntax
open Batteries
module Env = Collections.CidMap
module IntMap = Map.Make (Int)

open InterpSwitch

module State = struct

  (* Maps switch -> port -> (switch * port) *)
  type topology = (int * int) IntMap.t IntMap.t

  let empty_topology num_switches =
    List.fold_left
      (fun acc n -> IntMap.add n IntMap.empty acc)
      IntMap.empty
      (List.init num_switches (fun n -> n))
  ;;

  type config =
    { max_time : int
    ; default_input_gap : int
    ; generate_delay : int
    ; propagate_delay : int
    ; random_seed : int
    ; random_delay_range : int
    ; random_propagate_range : int
    ; drop_chance : int
    }

  type network_state =
    { current_time : int
    ; config : config
    ; event_sorts : event_sort Env.t
    ; event_signatures  : (Cid.t * CoreSyntax.ty list) IntMap.t
    ; handlers : handler Env.t
    ; links : topology
    ; switches : switch array
    ; actions : action Env.t
    ; global_names : SyntaxGlobalDirectory.dir
    }
    and handler = network_state InterpSyntax.handler
    and switch = network_state InterpSwitch.state
  
    type code = network_state InterpSyntax.code

  type global_fun =
    { cid : Cid.t
    ; body : code
    ; ty : Syntax.ty
        (* Not CoreSyntax.ty, since this is needed only for type inference *)
    }

  let create config : network_state =
    { current_time = -1
    ; config
    ; event_sorts = Env.empty
    ; event_signatures = IntMap.empty
    ; handlers = Env.empty
    ; switches = Array.of_list []
    ; links = empty_topology 0
    ; actions = Env.empty
    ; global_names = SyntaxGlobalDirectory.empty_dir
    }
  ;;

  (* switch wrappers *)

  let sw nst swid = nst.switches.(swid)
  let mem_env swid cid nst = InterpSwitch.mem_env cid nst.switches.(swid)
  let lookup swid k nst = InterpSwitch.lookup k nst.switches.(swid)


  let add_global swid cid v nst =
    let st = InterpSwitch.add_global cid v nst.switches.(swid) in
    nst.switches.(swid) <- st
  ;;

  let add_global_function (g : global_fun) nst =
    Array.modify
      (fun st ->
        if InterpSwitch.mem_env g.cid st
        then
          error ("global variable " ^ Cid.to_string g.cid ^ "  already defined")
        else InterpSwitch.add_global g.cid (F g.body) st)
      nst.switches
  ;;

  let lookup_handler cid nst =
    try Some (Env.find cid nst.handlers) with
    | Not_found -> error ("missing handler: " ^ Cid.to_string cid)
  ;;

  let lookup_action cid nst =
    try Env.find cid nst.actions with
    | Not_found -> error ("missing action: " ^ Cid.to_string cid)
  ;;

  let add_handler cid lam nst =
    { nst with handlers = Env.add cid lam nst.handlers }
  ;;

  let add_action cid action nst =
    { nst with actions = Env.add cid action nst.actions }
  ;;

  let log_exit swid port event nst =
    InterpSwitch.log_exit (sw nst swid) port event nst.current_time
  ;;

  let log_drop swid event nst =
    InterpSwitch.log_drop (sw nst swid) event nst.current_time
  ;;

  let update_counter swid event nst =
    let st = nst.switches.(swid) in
    let event_sort = Env.find event.eid nst.event_sorts in
    InterpSwitch.update_counter st event_sort
  ;;

  (* push a single event to a single switch's queue, used for
     events generated by the program itself. *)
  let push_generated_event src_id dst_id port (ievent : interp_event) nst =
    let delay = InterpSyntax.delay ievent in
    let st = nst.switches.(dst_id) in
    let propagate_delay =
      if src_id = dst_id
      then
        nst.config.propagate_delay
        + Random.int nst.config.random_propagate_range
      else 0
    in
    let t =
      nst.current_time
      + max delay nst.config.generate_delay
      + propagate_delay
      + Random.int nst.config.random_delay_range
    in    
    if Random.int 100 < nst.config.drop_chance
    then (InterpSwitch.log_drop st ievent nst.current_time)
    else (nst.switches.(dst_id) <- InterpSwitch.push_event st ievent t port;)
  ;;

  (* push an event from the user, which may be a program event
     or a control event. Doesn't add delays. *)
  let push_input_event (loc : loc) (event : interp_event) nst =
    let st = nst.switches.(loc.switch) in
    nst.switches.(loc.switch) <- InterpSwitch.push_event st event (interp_event_delay event) loc.port
  ;;

  (* Push an event to a list of entry points *)
  let push_interp_events locs event nst =
    let push_wrapper loc = push_input_event loc event nst in
    List.iter push_wrapper locs
  ;;

  (* push a located event to multiple switches where it should appear *)
  let push_located_events (located_events : located_event list) nst =
    let wrapper located_event =
      push_interp_events located_event.ilocs located_event.ievent nst
    in
    List.iter wrapper located_events
  ;;

  let next_event swid nst =
    let st = nst.switches.(swid) in
    match InterpSwitch.next_event st nst.current_time with
      | None -> None
      | Some(st, event, port) -> 
        nst.switches.(swid) <- st;
        Some(event, port)
  ;;

  let next_time nst =
    Array.fold_left
      (fun acc st ->
        match acc, InterpSwitch.next_time st with
        | None, x | x, None -> x
        | Some x, Some y -> Some (min x y))
      None
      nst.switches
  ;;

  let get_table_entries_switch swid stage nst =
    InterpSwitch.get_table_entries (sw nst swid) stage
  ;;

  let install_table_entry_switch swid stage nst entry =
    InterpSwitch.install_table_entry (sw nst swid) stage entry
  ;;

  let update_switch swid stage idx getop setop nst =
    InterpSwitch.update (sw nst swid) stage idx getop setop
  ;;

  let update_switch_complex swid stage idx memop nst =
    InterpSwitch.update_complex (sw nst swid) stage idx memop
  ;;

  (* Maps switch * port -> switch * port according to the topology *)
  let lookup_dst nst (sw, p) =
    match IntMap.find_opt sw nst.links with
    | Some map ->
      (match IntMap.find_opt p map with
       | None -> -1, p
       | Some ret -> ret)
    (* the switch ID does not exist in the topology... *)
    | None ->
      error @@ "lookup_dst error -- Invalid switch id " ^ string_of_int sw
  ;;

  let ival_to_string v =
    match v with
    | V v -> CorePrinting.value_to_string v
    | F _ -> "<function>"
    | P pv -> InterpSyntax.payload_to_string pv
  ;;


  let env_to_string env =
    if Env.is_empty env
    then "{ }"
    else
      Printf.sprintf "{\n%s  }"
      @@ Env.fold
           (fun id v acc ->
             let kstr = Cid.to_string id in
             acc ^ "    " ^ kstr ^ " = " ^ ival_to_string v ^ ";\n")
           env
           ""
  ;;
  let event_queue_to_string = InterpSwitch.event_queue_to_string 
  let exits_to_string = InterpSwitch.exits_to_string 
  let drops_to_string = InterpSwitch.drops_to_string
  let stats_counter_to_string = InterpSwitch.stats_counter_to_string

  let st_to_string = (InterpSwitch.to_string ival_to_string)

  let nst_to_string
    ?(show_vars = false)
    ?(show_pipeline = true)
    ?(show_queue = true)
    ?(show_exits = true)
    nst
    =
    Array.fold_lefti
      (fun acc idx st ->
        Printf.sprintf "%s\nSwitch %d : %s" acc idx
        @@ st_to_string ~show_vars ~show_pipeline ~show_queue ~show_exits st)
      ""
      nst.switches
  ;;
end

let extract_ival iv =InterpSyntax.extract_ival iv
;;
